const NUM_PILOTS = 2
const NUM_TUGS = 5
const DOCKING_TUGS = 3
const UNDOCKING_TUGS = 2
const CAPACITY_ARRIVAL_ZONE = 1
const CAPACITY_DEPARTURE_ZONE = 1

const False = 0
const True = 1
range Bool = False..True

PILOT = (get_a_cargo_ship -> acquire_docking_tugs -> dock -> release_docking_tugs
    -> unload -> acquire_undocking_tugs -> undock
    -> release_undocking_tugs -> release_a_cargo_ship -> PILOT ).

ARRIVAL_ZONE = ARRIVAL_ZONE[0],
ARRIVAL_ZONE[i : 0..CAPACITY_ARRIVAL_ZONE] = 
    (when (i > 0) arrival_zone_release -> ARRIVAL_ZONE[i - 1]
    |when(i < CAPACITY_ARRIVAL_ZONE) arrival_zone_get -> ARRIVAL_ZONE[i + 1]).

DEPARTURE_ZONE = DEPARTURE_ZONE[0],
DEPARTURE_ZONE[i : 0..CAPACITY_DEPARTURE_ZONE] = 
    (when (i > 0) departure_zone_release -> DEPARTURE_ZONE[i - 1]
    |when(i < CAPACITY_DEPARTURE_ZONE) departure_zone_get -> DEPARTURE_ZONE[i + 1]).

BERTH = BERTH[True][False],
BERTH[shield_status : Bool][ship_in : Bool] = (when (shield_status == False) activate -> BERTH[True][ship_in]
    |when (shield_status == False && ship_in == False) dock -> BERTH[shield_status][True]
	|when (shield_status == False && ship_in == True) undock -> BERTH[shield_status][False]
    |when(shield_status == True) deactivate -> BERTH[False][ship_in]).

TUGS = TUGS[NUM_TUGS],
TUGS[i : 0..NUM_TUGS] = (when (i - DOCKING_TUGS >= 0) acquire_docking_tugs -> TUGS[i - DOCKING_TUGS]
    |when (i - UNDOCKING_TUGS >= 0) acquire_undocking_tugs -> TUGS[i - UNDOCKING_TUGS]
    |when (i + DOCKING_TUGS <= NUM_TUGS) release_docking_tugs -> TUGS[i + DOCKING_TUGS]
    |when (i + UNDOCKING_TUGS <= NUM_TUGS) release_undocking_tugs -> TUGS[i + UNDOCKING_TUGS]).

||USS_EMAFOR = (pilot[0..NUM_PILOTS - 1]:PILOT 
    || pilot[0..NUM_PILOTS - 1]::(ARRIVAL_ZONE/{get_a_cargo_ship/arrival_zone_release})
	|| pilot[0..NUM_PILOTS - 1]::(DEPARTURE_ZONE/{release_a_cargo_ship/departure_zone_get})
    || pilot[0..NUM_PILOTS - 1]::TUGS
    || pilot[0..NUM_PILOTS - 1]::BERTH).


